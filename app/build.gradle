apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "org.sagebionetworks.research.crf"
        minSdkVersion 19
        targetSdkVersion 26
	// version code is bumped by fastlane automatically
        versionCode 32
	// this should match the stable- branch name and will be used by fastlane to create the tag
        versionName "1.5.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        resConfigs "en"

        renderscriptTargetApi 18
        renderscriptSupportModeEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        warning "MissingTranslation"
        abortOnError false
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:multidex:1.0.2'

    // These are all support libraries that should be updated when Google releases new ones
    implementation "com.android.support:support-compat:26.1.0"
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:preference-v7:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:support-annotations:26.1.0'
    implementation 'com.android.support:customtabs:26.1.0'

    implementation 'net.openid:appauth:0.7.0'

    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.google.guava:guava:23.2-android'

    implementation 'com.google.dagger:dagger-android:2.14.1'
    implementation 'com.google.dagger:dagger-android-support:2.14.1'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.14.1'
    annotationProcessor "com.google.dagger:dagger-compiler:2.14.1"

    implementation project(':researchstack-sdk')

    // Important - the CameraSource implementation in this project requires version 8.1 or higher.
    implementation 'com.google.android.gms:play-services-vision:11.6.2'

    testImplementation project(':test-utils')
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.5'
    // Keep these older versions, since the CrfDataProvider tests relies on them
    testImplementation 'org.mockito:mockito-core:2.8.9'
    testImplementation 'org.powermock:powermock-api-mockito2:1.7.1'
    testImplementation 'org.powermock:powermock-module-junit4-rule-agent:1.7.1'
    testImplementation 'org.powermock:powermock-module-junit4-rule:1.7.1'
    testImplementation 'org.powermock:powermock-module-junit4:1.7.1'

    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
}
